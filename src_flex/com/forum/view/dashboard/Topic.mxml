<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="100%" height="35%" 
		  title="Topics ({this.topics.length})"
		  xmlns:service="model.services.*"
		  >
	
	
	<mx:Script>
		<![CDATA[
			import model.vo.CategoryVO;
			import model.vo.TopicVO;
			import model.vo.UserVO;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.ItemClickEvent;
			import mx.events.ListEvent;
			import mx.messaging.messages.RemotingMessage;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]
			public var topics:ArrayCollection;
			
			[Bindable]
			public var user:UserVO;
			
			private var _currentCategory:CategoryVO;
			
			
			public function get currentCategory():CategoryVO
			{
				return _currentCategory;
			}
			
			[Bindable]
			public function set currentCategory(cat:CategoryVO):void
			{
				_currentCategory = cat;	
				
				if(cat == null){
					topics = new ArrayCollection(); //Clear the topics if a category isn't selected.
				}else if(cat.name == "Uncategorized"){
					//Need to figure out how to retrieve orphaned topics.
					this.topics = new ArrayCollection();
				}else if(cat != null){
					this.topicService.getAllTopicWithRelated({"categoryId":this.currentCategory.id});
				}
			}
			
			
			private function resultHandler(event:ResultEvent):void
			{
				var message:RemotingMessage = event.token.message as RemotingMessage;
				var operation:String = message.source + "." + message.operation;
				
				switch(operation)
				{
					
					case topicService.GET_ALL_TOPICS:
						this.topics = event.result as ArrayCollection;
						break;
					case topicService.SAVE_TOPIC:
						this.topicService.getAllTopicWithRelated({"categoryId":this.currentCategory.id});
						break;
					case topicService.UPDATE_TOPIC:
						this.topicService.getAllTopicWithRelated({"categoryId":this.currentCategory.id});
						break;
					case topicService.DELETE_TOPIC:
						this.topicService.getAllTopicWithRelated({"categoryId":this.currentCategory.id});
						break;
					case topicService.GET_ALL_TOPIC_WITH_RELATED:
						this.topics = (event.result as ArrayCollection);
						break;
				}
			}
			
			private function faultHandler(event:FaultEvent):void
			{
				Alert.show("Error in Topic.mxml: " + event.fault.faultString);
			}
			
			public function insertTopic(name:String, description:String):void
			{
				var topic:TopicVO = new TopicVO();
				if(this.currentCategory) topic.categoryId = this.currentCategory.id;
				topic.userId = this.user.id;
				topic.name = name;
				topic.description = description;
				this.topicService.saveTopic(topic);
			}

			
			public function updateTopic(name:String, description:String):void
			{
				var topic:TopicVO = new TopicVO();
				topic.name = name;
				topic.description = description;
				topic.id = (this.topicList.selectedItem as TopicVO).id;
				
				//I shouldn't hae to add the following on an UPDATE.
				if(this.currentCategory) topic.categoryId = (this.topicList.selectedItem as TopicVO).categoryId;
				topic.userId = (this.topicList.selectedItem as TopicVO).userId;
				
				this.topicService.updateTopic(topic);
			}
			
			public function deleteTopic():void
			{
				var topic:TopicVO = this.topicList.selectedItem as TopicVO;
				this.topicService.deleteTopic(topic);
			}
			
			public function clickTopicItem(event:ListEvent):void
			{
				this.topicName.text = (event.itemRenderer.data as TopicVO).name;
				this.topicDescription.text = (event.itemRenderer.data as TopicVO).description;
			}
		

		]]>
	</mx:Script>
	
	<service:TopicService id="topicService" result="resultHandler(event)" fault="faultHandler(event)"/>
	
	<mx:DataGrid id="topicList" left="0" right="0" top="0" bottom="0" dataProvider="{this.topics}" change="clickTopicItem(event)">
		<mx:columns>
			<mx:DataGridColumn headerText="Topic Name" dataField="name"/>
			<mx:DataGridColumn headerText="Description" dataField="description"/>
			<mx:DataGridColumn headerText="Category" dataField="categoryId"/>
			<mx:DataGridColumn headerText="User" dataField="userId"/>
		</mx:columns>
	</mx:DataGrid>
	
	
	<mx:ControlBar>
		<mx:HBox>
			<mx:Label text="Topic Name:" />
			<mx:TextInput id="topicName" text="" />
			<mx:Label text="Description" />
			<mx:TextInput id="topicDescription" text="" />
			<mx:Button label="Add" click="insertTopic(this.topicName.text, this.topicDescription.text)" enabled="{Boolean(currentCategory != null)}"  />
			<mx:Button label="Update" click="updateTopic(this.topicName.text, this.topicDescription.text)"  enabled="{Boolean(topicList.selectedItem != null)}" />
			<mx:Button label="Delete" click="deleteTopic()" enabled="{Boolean(topicList.selectedItem != null)}" />
		</mx:HBox>
	</mx:ControlBar>
	
</mx:Panel>
