<?xml version="1.0" encoding="utf-8"?>
<mx:Panel xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" width="100%" minWidth="210" height="316" borderStyle="solid" creationComplete="init()"
		  title="Categories({this.categories.length})"
		  xmlns:service="model.services.*"
		  >
	
	<mx:Script>
		<![CDATA[
			import model.vo.CategoryVO;
			import model.vo.UserVO;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.ItemClickEvent;
			import mx.events.ListEvent;
			import mx.messaging.messages.RemotingMessage;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			[Bindable]
			public var user:UserVO;
			
			private var _categories:ArrayCollection;
			
			
			public function init():void
			{
				this.categoryService.getAllCategories();
			}
			
			public function get categories():ArrayCollection
			{
				return _categories;
			}
			
			[Bindable]
			public function set categories(cats:ArrayCollection):void
			{
				_categories = cats;
				//Add "Uncategorized" to the list
				var cat:CategoryVO = new CategoryVO();
				cat.name = "Uncategorized";
				_categories.addItemAt(cat,0);
				
			}
			
			public function insertCategory(name:String):void
			{
				if(name == "Uncategorized"){
					Alert.show("Uncategorized is reserved.  Please try another name.");
				}else{
					var cat:CategoryVO = new CategoryVO();
					cat.name = name;
					cat.userId = this.user.id;
					this.categoryService.saveCategory(cat);			
				}
			}
			
			
			public function updateCategory(name:String):void
			{
				var cat:CategoryVO = new CategoryVO();
				cat.id = (this.categoryList.selectedItem as CategoryVO).id;
				cat.name = name;
				
				//I shouldn't have to add the following on an UPDATE.
				cat.userId = (this.categoryList.selectedItem as CategoryVO).userId
				
				this.categoryService.updateCategory(cat);			
			}
			
			public function deleteCategory():void
			{
				var cat:CategoryVO = this.categoryList.selectedItem as CategoryVO;
				this.categoryService.deleteCategory(cat);
			}
			
			
			public function clickCategoryItem(event:ListEvent):void
			{
				this.categoryName.text = (event.itemRenderer.data as CategoryVO).name;
			}
			
			
			private function resultHandler(event:ResultEvent):void
			{
				var message:RemotingMessage = event.token.message as RemotingMessage;
				var operation:String = message.source + "." + message.operation;
				
				switch(operation)
				{
					
					case categoryService.GET_ALL_CATEGORIES:
						this.categories = event.result as ArrayCollection;
						break;
					case categoryService.SAVE_CATEGORY:
						this.categoryService.getAllCategories();
						break;
					case categoryService.UPDATE_CATEGORY:
						this.categoryService.getAllCategories();
						break;
					case categoryService.DELETE_CATEGORY:
						this.categoryService.getAllCategories();
						break;
				}
			}
			
			
			private function faultHandler(event:FaultEvent):void
			{
				Alert.show("Error in Category.mxml: " + event.fault.faultString);
			}
			
		]]>
	</mx:Script>
	
	<service:CategoryService id="categoryService" result="resultHandler(event)" fault="faultHandler(event)"/>
	<mx:List id="categoryList" dataProvider="{this.categories}" labelField="name" top="0" bottom="0" left="0" right="0" change="clickCategoryItem(event)" borderStyle="none"/>
	
	<mx:ControlBar>
		<mx:VBox>
			<mx:HBox>
				<mx:TextInput id="categoryName"  width="194"/>
				<mx:Spacer width="100%"/>
			</mx:HBox>
			<mx:HBox>
				<mx:Button label="Add" click="insertCategory(this.categoryName.text)" />
				<mx:Button label="Update" click="updateCategory(this.categoryName.text)"  enabled="{Boolean( categoryList.selectedIndex > 0)}" />
				<mx:Button label="Delete" click="deleteCategory()" enabled="{Boolean(categoryList.selectedIndex > 0)}" />
				
			</mx:HBox>
		</mx:VBox>
		
	</mx:ControlBar>
	
</mx:Panel>
